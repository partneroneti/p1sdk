plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group 'br.com.partner1'
version '1.0'

android {
    compileSdk 32

    defaultConfig {
        minSdk 23
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.otaliastudios:cameraview:2.7.2'

    implementation ('com.facetec:facetec-sdk:9.6.4@aar')
}


afterEvaluate {
    publishing{
        publications{
            release(MavenPublication){
                from components.release
                groupId = group
                artifactId = 'photoface'
                version = version
                pom.withXml {
                    asNode().dependencies
                            .dependency
                            .findAll { dependency ->
                                dependency.groupId.text() == 'com.facetec'
                            }
                            .each { dependency ->
                                dependency.groupId*.value = group
                                dependency.artifactId*.value = 'photoface-dep'
                                dependency.version*.value = version
                                dependency.remove(dependency.type)
                            }
                }
            }
            facetec(MavenPublication) {
                artifact("$rootDir/libs/facetec-sdk-9.6.4.aar")
                groupId group
                artifactId 'photoface-dep'
                version version
            }
        }
    }
}

